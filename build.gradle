plugins {
  id 'eclipse'
  id 'groovy'
  // Apply the Plugin Publish Plugin to make plugin publication possible
  id 'com.gradle.plugin-publish' version '1.0.0'
  id 'com.github.spotbugs' version '5.0.13'
  id 'se.solrike.sonarlint' version '1.0.0-beta.8'
}

repositories {
  mavenCentral()
}
// latest core ? 8.11.0.56591
dependencies {
  implementation('org.apache.commons:commons-text:1.9')
  implementation('org.sonarsource.sonarlint.core:sonarlint-core:8.0.2.42487')

  sonarlintPlugins 'org.sonarsource.java:sonar-java-plugin:7.17.0.31219'

  testImplementation('org.mockito:mockito-core:3.7.7')
  testImplementation('org.mockito:mockito-junit-jupiter:3.7.7')

  testImplementation('org.assertj:assertj-core:3.22.0')
  testImplementation('org.junit.jupiter:junit-jupiter')
  testImplementation(platform('org.junit:junit-bom:5.8.2'))
}

group = 'se.solrike.sonarlint'
version = '1.0.0-beta.9'
sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(GroovyCompile) {
  configure(options) {
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
  }
}

sourceSets {
  main {
    java {
      srcDirs = [] // don't compile Java code twice
    }
    groovy {
      srcDirs = [
        'src/main/groovy',
        'src/main/java'
      ]
    }
  }
}

javadoc {
  enabled = false
}
groovydoc {
  enabled = false
}

// folder names that ends with 'nosync' will not be synced to iCloud drive.
eclipse {
  classpath {
    def mainOutputDir = 'bin.nosync/main'
    defaultOutputDir = file(mainOutputDir)
    file {
      whenMerged {
        entries.findAll({it.kind == 'src'}).each {
          if (it.path.contains('main') || it.path == '.apt_generated') {
            it.output = mainOutputDir
          }
          else if (it.path.contains('test')) {
            it.output = 'bin.nosync/test'
          }
        }
      }
    }
  }
}

test {
  useJUnitPlatform()
}

// Spotbugs
spotbugsMain {
  effort = 'less' // 'default', 'less' or 'max'
  excludeFilter = file('./plugin-config/spotbugs/exclude.xml')
  ignoreFailures = true
  showStackTraces = false
  dependencies {
    // http://find-sec-bugs.github.io
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'
    spotbugs 'com.github.spotbugs:spotbugs:4.7.1'
  }
  reports {
    xml.enabled = true
    html.enabled = true
  }
}
spotbugsTest {
  enabled = false
}

sonarlintMain {
  reports {
    text.enabled = true
  }
  ignoreFailures = true
}


// publish the plugin like: ./gradlew publishPlugins
gradlePlugin {
  plugins {
    sonarlintPlugin {
      id = group
      implementationClass = 'se.solrike.sonarlint.SonarlintPlugin'
      displayName = 'Sonarlint plugin'
      description = 'Sonarlint static code analysis using stand-alone Sonarlint engine, for ' +
          'Java, Kotlin, Scala and Node.js projects. But other languages are also possible to lint like Ruby and Golang. ' +
          '\nIt is possible to include and exclude rules and also list the rules in the Sonarlist plugins you depend on. ' +
          '\nThe plugin can produce Spotbugs compatible XML reports which can be used by Github actions or Jenkins. ' +
          '\nMin Gradle version 7.0.'
    }
  }
}

pluginBundle {
  // These settings are set for the whole plugin bundle
  website = 'https://github.com/Lucas3oo/sonarlint-gradle-plugin'
  vcsUrl = website
  pluginTags = [
    sonarlintPlugin: [
      'code analysis',
      'code quality',
      'codesmells',
      'java',
      'kotlin',
      'lint',
      'node',
      'scala',
      'sonarlint',
      'spotbugs',
      'static analysis',
      'static code analysis'
    ]
  ]
}

// to upgrade the metainfo about which gradle version we shall use. Run ./gradlew wrapper after you have updated the version. The commit the new wrapper files except the gradlew.bat
wrapper {
  gradleVersion = '7.5.1'
}
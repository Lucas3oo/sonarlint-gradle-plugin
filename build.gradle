plugins {
  id 'eclipse'
  id 'groovy'
  // Apply the Plugin Publish Plugin to make plugin publication possible
  id 'com.gradle.plugin-publish' version '1.0.0'
}

repositories {
  mavenCentral()
}
// latest core ? 8.11.0.56591
dependencies {
  implementation 'org.apache.commons:commons-text:1.9'
  implementation 'org.sonarsource.sonarlint.core:sonarlint-core:8.0.2.42487'
  testImplementation('org.assertj:assertj-core:3.22.0')
  testImplementation('org.junit.jupiter:junit-jupiter')
  testImplementation(platform('org.junit:junit-bom:5.8.2'))
}

group = 'se.solrike.sonarlint'
version = '1.0.0-beta.8'
sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(GroovyCompile) {
  configure(options) {
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
  }
}

sourceSets {
  main {
    java {
      srcDirs = [] // don't compile Java code twice
    }
    groovy {
      srcDirs = [
        'src/main/groovy',
        'src/main/java'
      ]
    }
  }
}

javadoc {
  enabled = false
}
groovydoc {
  enabled = false
}

// folder names that ends with 'nosync' will not be synced to iCloud drive.
eclipse {
  classpath {
    def mainOutputDir = 'bin.nosync/main'
    defaultOutputDir = file(mainOutputDir)
    file {
      whenMerged {
        entries.findAll({it.kind == 'src'}).each {
          if (it.path.contains('main') || it.path == '.apt_generated') {
            it.output = mainOutputDir
          }
          else if (it.path.contains('test')) {
            it.output = 'bin.nosync/test'
          }
        }
      }
    }
  }
}

test {
  useJUnitPlatform()
}


// publish the plugin like: ./gradlew publishPlugins
gradlePlugin {
  plugins {
    sonarlintPlugin {
      id = group
      implementationClass = 'se.solrike.sonarlint.SonarlintPlugin'
      displayName = 'Sonarlint plugin'
      description = 'Sonarlint static code analysis using stand-alone Sonarlint engine, for ' +
          'Java, Kotlin, Scala and Node projects. But other languages are also possible to lint. ' +
          '\nIt is possible to include and exclude rules and also list the rules in the Sonarlist plugins you depend on. ' +
          '\nMin Gradle version 7.0.'
    }
  }
}

pluginBundle {
  // These settings are set for the whole plugin bundle
  website = 'https://github.com/Lucas3oo/sonarlint-gradle-plugin'
  vcsUrl = website
  pluginTags = [
    sonarlintPlugin: [
      'sonarlint',
      'java',
      'kotlin',
      'scala',
      'node',
      'code quality',
      'lint',
      'static code analysis',
      'static analysis',
      'code analysis',
      'codesmells'
    ]
  ]
}

// to upgrade the metainfo about which gradle version we shall use. Run ./gradlew wrapper after you have updated the version. The commit the new wrapper files except the gradlew.bat
wrapper {
  gradleVersion = '7.5.1'
}